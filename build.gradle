/**
 * Gradle Build file for OpenDial (based on a previous file written by Ingmar Steiner).
 */


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
mainClassName = 'opendial.DialogueSystem'
 
repositories {
    flatDir {
    	dirs file("${projectDir}/lib")
    }
    maven {
        url "https://jcenter.bintray.com"
    }
    maven {
        url 'https://oss.jfrog.org/artifactory/repo'
    }
    maven {
    	url 'https://oss.sonatype.org/content/repositories/repositories/snapshots'
    }
}


sourceSets {
	main {
		java {
			srcDirs 'src'
			exclude 'opendial/plugins/MaryTTS.java'
			exclude 'opendial/plugins/SphinxASR.java'
	    }
	}
	test {
	    java {
			srcDirs 'test'
			exclude 'domains/**'
	    }
	}
	plugins {
    	java {
      		srcDirs 'src'
      		include 'opendial/plugins/MaryTTS.java'
      		include 'opendial/plugins/SphinxASR.java'
    	}
    	compileClasspath+=sourceSets.main.output
  	}
}



dependencies {
	compile group:'org.apache.httpcomponents', name:'httpclient', version:'4.3+'
	compile group:'net.java.balloontip', name:'balloontip', version:'1.2+'
	compile group:'org.json', name:'org.json', version:'2.0'
	compile group:'org.jfree', name:'jfreechart', version:'1.0.19+'
	compile group:'net.sf.jung', name:'jung-graph-impl', version:'2.0.1'
	compile group:'net.sf.jung', name:'jung-visualization', version:'2.0.1'
	compile name: 'att-codekit'
	compile name: 'exp4j'
	testCompile group:'junit', name: 'junit',  version: '4.+'
 	pluginsCompile group: 'de.dfki.mary', name: 'marytts-runtime', version: '5.1.1'
 	pluginsCompile group: 'de.dfki.mary', name: 'voice-cmu-slt-hsmm', version: '5.1.1'
    pluginsCompile group: 'org.apache.commons', name: 'commons-math3', version: '3.2'
    pluginsCompile group: 'edu.cmu.sphinx', name: 'sphinx4-core', version:'1.0-SNAPSHOT'
  }


tasks.withType(JavaCompile) {
	sourceCompatibility = '1.8'
	options.encoding = 'UTF-8'
}

run {
     systemProperties System.getProperties()
	systemProperty 'file.encoding', 'UTF-8'
}

test {
  	testLogging {
		exceptionFormat = 'full'
    }
  	systemProperty 'file.encoding', 'UTF-8'
}

jar {
    manifest {
		attributes('Main-Class': project.mainClassName)
    }
}
